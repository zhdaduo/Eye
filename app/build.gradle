apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def prop = new Properties()
prop.load(rootProject.file("app/sampledata/signature.properties").newDataInputStream())

android {
    signingConfigs {
        release {
            storeFile file(prop['STORE_FILE'])
            storePassword prop['STORE_PASSWORD']
            keyAlias prop['KEY_ALIAS']
            keyPassword prop['KEY_PASSWORD']
        }
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.mor.eye"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.2.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            ndk {
                //APP的build.gradle设置支持的SO库架构
                abiFilters 'armeabi', 'armeabi-v7a', 'x86'
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk {
                //APP的build.gradle设置支持的SO库架构
                abiFilters 'armeabi', 'armeabi-v7a'
            }
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def fileName = "EYE_${variant.versionName}_release.apk"
                    def outFile = output.outputFile
                    if (outFile != null && outFile.name.endsWith('.apk')) {
                        outputFileName = fileName
                    }
                }
            }
        }
    }
    dataBinding {
        enabled = true
    }
    androidExtensions {
        experimental = true
    }
}



dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:palette-v7:27.1.1'

    // Koin
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-scope:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"

    // gson
    implementation 'com.google.code.gson:gson:2.8.1'

    // Networking
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // ViewModel & LiveData & room & paging
    implementation "android.arch.lifecycle:extensions:$androidArchitectureVersion"
    kapt "android.arch.lifecycle:compiler:$androidArchitectureVersion"
    implementation "android.arch.persistence.room:runtime:$androidArchitectureVersion"
    kapt "android.arch.persistence.room:compiler:$androidArchitectureVersion"
    implementation "android.arch.persistence.room:rxjava2:$androidArchitectureVersion"
    implementation 'android.arch.paging:runtime:1.0.1'

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // epoxy
    implementation "com.airbnb.android:epoxy:$epoxy_version"
    implementation "com.airbnb.android:epoxy-databinding:$epoxy_version"
    kapt "com.airbnb.android:epoxy-processor:$epoxy_version"

    // leaks
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    // glide
    implementation 'com.github.bumptech.glide:glide:4.4.0'
    kapt 'com.github.bumptech.glide:compiler:4.4.0'
    implementation 'jp.wasabeef:glide-transformations:3.0.1'

    // tab layout
    implementation 'com.ogaclejapan.smarttablayout:library:1.6.1@aar'
    implementation 'com.ogaclejapan.smarttablayout:utils-v4:1.6.1@aar'

    // logger
    implementation 'com.orhanobut:logger:2.2.0'

    // video
    implementation('com.shuyu:GSYVideoPlayer:6.0.1') {
        exclude group: 'com.android.support', module: 'animated-vector-drawable'
        exclude group: 'com.android.support', module: 'support-media-compat'
        exclude group: 'com.android.support', module: 'support-v4'
    }

    // photoView
    implementation 'com.github.chrisbanes:PhotoView:2.1.4'

    // stetho
    debugImplementation 'com.facebook.stetho:stetho:1.3.1'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.4.1'

    // vlayout
    implementation 'com.alibaba.android:vlayout:1.0.6@aar'

    // immers bar
    implementation 'com.gyf.barlibrary:barlibrary:2.3.0'

    // fragmentation
    implementation 'me.yokeyword:fragmentation:1.3.6'

    // bottom bar
    implementation 'me.majiajie:pager-bottom-tab-strip:2.3.0'
    
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
